(* Copyright 2014 Gabriele Mambrini 

 This grammar is for Grako (https://bitbucket.org/apalala/grako).

*)

configuration =
    rules:{rule}+ $
    ;

rule =
    'rule' name:identifier conditions:condition_list actions:action_list 'end'
    ;

condition_list =
    @+:condition {';' @+:condition }
    ;

condition =
    'match' test:test [ [ 'repeated' repeat:INTEGER 'times' ] 'within' limit:time_limit ] 
    ;

time_limit =
    INTEGER ( 's' | 'm' | 'h' )
    ;

test = 
    or_test
    ;

or_test =
    and_test {'or' and_test}
    ;

and_test =
    not_test {'and' not_test}
    ;

not_test =
    'not' not_test | comparison
    ;

comparison =
    identifier comp_op expr
    | identifier '~'   REGEX
    | identifier '!~'  REGEX
    ;

comp_op =
      '<'
    | '>'
    | '='
    | '>='
    | '<='
    | '!='
    | 'in'
    | 'not' 'in'
    ;

expr = 
      INTEGER
    | STRING
    | identifier
    ;

action_list =
    @+:action { ';' @+:action }
    ;


(* TODO action parameters *)
action =
     'action' @:identifier
     ;


(* just for test *)
identifier  =
    FRAGMENT
    ;

FRAGMENT = 
    ?/[a-zA-Z][a-zA-Z0-9]+/? 
    ;

INTEGER =
    ?/[0-9]+/?
    ;

STRING =
    '"'  @:{?/[^"\n\\]/?|ESC} '"'
    |
    "'"  @:{?/[^'\n\\]/?|ESC} "'"
    ;

REGEX =
      '?/' ~ @:?/(.|\n)+?(?=/\?)/?? ?//\?+/?? ~
    | '/' ~ @:?/(.|\n)+?(?=/)/? '/' ~
    ;


ESC =
    ?/\\['"\\nrtbfv]/?
    |
    ?/\\u[a-fA-F0-9]{4}/?
    ;
